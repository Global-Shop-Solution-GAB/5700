Program.Sub.ScreenSU.Start
Gui.frmQuoteCreate..Create
Gui.frmQuoteCreate..Caption("Quote Creation")
Gui.frmQuoteCreate..Size(7095,5520)
Gui.frmQuoteCreate..MinX(0)
Gui.frmQuoteCreate..MinY(0)
Gui.frmQuoteCreate..Position(0,0)
Gui.frmQuoteCreate..BackColor(-2147483633)
Gui.frmQuoteCreate..MousePointer(0)
Gui.frmQuoteCreate..Event(UnLoad,frmQuoteCreate_UnLoad)
Gui.frmQuoteCreate.lbl1.Create(Label,"Step 2: Drag and Drop CSV File Here",True,6495,255,0,120,3870,True,0,"Arial",8,-2147483633,0)
Gui.frmQuoteCreate.lbl1.FontStyle(True,False,False,False,False)
Gui.frmQuoteCreate.lbl2.Create(Label,"Step 1: Select Customer",True,4470,255,0,165,240,True,0,"Arial",8,-2147483633,0)
Gui.frmQuoteCreate.lbl2.FontStyle(True,False,False,False,False)
Gui.frmQuoteCreate.txtCustomer.Create(TextBox,"",True,1365,300,0,165,540,True,0,"Arial",8,-2147483643,1)
Gui.frmQuoteCreate.cmdBrowse.Create(Button)
Gui.frmQuoteCreate.cmdBrowse.Size(855,375)
Gui.frmQuoteCreate.cmdBrowse.Position(1650,510)
Gui.frmQuoteCreate.cmdBrowse.Caption("^")
Gui.frmQuoteCreate.cmdBrowse.Event(Click,cmdBrowse_Click)
Gui.frmQuoteCreate.ddlEstimate.Create(DropDownList)
Gui.frmQuoteCreate.ddlEstimate.Size(3000,330)
Gui.frmQuoteCreate.ddlEstimate.Position(120,1965)
Gui.frmQuoteCreate.ddlExpiration.Create(DropDownList)
Gui.frmQuoteCreate.ddlExpiration.Size(2970,330)
Gui.frmQuoteCreate.ddlExpiration.Position(135,2640)
Gui.frmQuoteCreate.ddlPayment.Create(DropDownList)
Gui.frmQuoteCreate.ddlPayment.Size(2985,330)
Gui.frmQuoteCreate.ddlPayment.Position(135,1260)
Gui.frmQuoteCreate.lblPayment.Create(Label,"Payment Terms",True,1935,255,0,165,990,True,0,"Arial",8,-2147483633,0)
Gui.frmQuoteCreate.lblExpiration.Create(Label,"Expiration/Validity",True,2955,255,0,165,2415,True,0,"Arial",8,-2147483633,0)
Gui.frmQuoteCreate.lblEstimate.Create(Label,"Estimate Lead Time",True,2940,255,0,165,1740,True,0,"Arial",8,-2147483633,0)
Gui.frmQuoteCreate.txtFileName.Create(TextBox,"",True,5925,300,0,120,4140,True,0,"Arial",8,-2147483643,1)
Gui.frmQuoteCreate.cmdBrowseFile.Create(Button)
Gui.frmQuoteCreate.cmdBrowseFile.Size(480,375)
Gui.frmQuoteCreate.cmdBrowseFile.Position(6195,4110)
Gui.frmQuoteCreate.cmdBrowseFile.Caption("^")
Gui.frmQuoteCreate.cmdBrowseFile.Event(Click,cmdBrowseFile_Click)
Gui.frmQuoteCreate.cmdUpload.Create(Button)
Gui.frmQuoteCreate.cmdUpload.Size(2475,375)
Gui.frmQuoteCreate.cmdUpload.Position(120,4500)
Gui.frmQuoteCreate.cmdUpload.Caption("Upload Quote/Sales Order")
Gui.frmQuoteCreate.cmdUpload.Event(Click,cmdUpload_Click)
Gui.frmQuoteCreate.optQuote.Create(Option)
Gui.frmQuoteCreate.optQuote.Size(1575,255)
Gui.frmQuoteCreate.optQuote.Position(150,3165)
Gui.frmQuoteCreate.optQuote.Caption("Quote")
Gui.frmQuoteCreate.optSale.Create(Option)
Gui.frmQuoteCreate.optSale.Size(1575,255)
Gui.frmQuoteCreate.optSale.Position(1695,3165)
Gui.frmQuoteCreate.optSale.Caption("Sales Order")
Gui.frmQuoteCreate.chkAutoPart.Create(CheckBox)
Gui.frmQuoteCreate.chkAutoPart.Size(4950,360)
Gui.frmQuoteCreate.chkAutoPart.Position(105,3465)
Gui.frmQuoteCreate.chkAutoPart.Caption("Auto Create Part Numbers (ex: CCCNNNNNNN-NNN)")
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
V.Global.sBuildData.Declare(string)
v.global.sConfigFile.Declare(string)
v.Global.lStartNumber.Declare(long)
Program.External.Include.Library("6004.lib")
Program.Sub.Preflight.End

Program.Sub.frmQuoteCreate_UnLoad.Start
f.ODBC.Connection!con.Close
f.Intrinsic.Control.End
Program.Sub.frmQuoteCreate_UnLoad.End

Program.Sub.Main.Start
Gui.frmQuoteCreate.trvDrag.Event("DRAGDROPFILE",Dragdrop)
f.ODBC.Connection!con.openconnection(v.Ambient.PDSN,v.Ambient.PUser,v.Ambient.PPass)
F.Intrinsic.Control.CallSub(loadConfig)
f.Intrinsic.Control.CallSub(loadPayments)
f.Intrinsic.Control.CallSub(loadExpiration)
f.Intrinsic.Control.CallSub(loadValidity)
f.Intrinsic.UI.SetBrowserHotTypeAhead(true)
gui.frmQuoteCreate.optQuote.Value(true)
f.Data.Dictionary.Create("dict")
gui.frmQuoteCreate..Show
Program.Sub.Main.End

Program.Sub.loadConfig.Start

'Determine if Config datatable exists, if so, close it
f.Intrinsic.Control.If(v.DataTable.dtConfig.Exists)
	f.Data.DataTable.Close("dtConfig")
f.Intrinsic.Control.EndIf
F.Intrinsic.String.Concat(V.Caller.GASDir,"\",V.Caller.CompanyCode,"_GAB_5700_CONFIG.xml",v.global.sConfigfile)
'Create datatable from config file
f.Data.DataTable.CreateFromXML("dtConfig",v.global.sConfigFile,true)

v.Global.lStartNumber.Set(v.DataTable.dtConfig(0).StartNumber!fieldval)

program.Sub.loadConfig.End

Program.Sub.loadPayments.Start
f.Data.DataTable.CreateFromSQL("dtP","con","SELECT TERMS_CODE, TERMS_MESSAGE from V_AR_TERMS_DESCR")
v.Local.i.Declare(long)
f.Intrinsic.Control.For(v.Local.i,0,v.DataTable.dtP.RowCount--,1)
	gui.frmQuoteCreate.ddlPayment.AddItem(v.DataTable.dtP(v.Local.i).TERMS_MESSAGE!FIELDVAL,v.DataTable.dtP(v.Local.i).TERMS_CODE!FIELDVAL)
f.Intrinsic.Control.Next(v.Local.i)
Program.Sub.loadPayments.End

Program.Sub.loadExpiration.Start
gui.frmQuoteCreate.ddlEstimate.AddItem("",0)
gui.frmQuoteCreate.ddlEstimate.AddItem("1 Week",0)
gui.frmQuoteCreate.ddlEstimate.AddItem("2 Weeks",1)
gui.frmQuoteCreate.ddlEstimate.AddItem("3 Weeks",2)
gui.frmQuoteCreate.ddlEstimate.AddItem("4 Weeks",3)
gui.frmQuoteCreate.ddlEstimate.AddItem("5 Weeks",4)
gui.frmQuoteCreate.ddlEstimate.AddItem("6 Weeks",5)
gui.frmQuoteCreate.ddlEstimate.AddItem("7 Weeks",6)
gui.frmQuoteCreate.ddlEstimate.AddItem("8 Weeks",7)
gui.frmQuoteCreate.ddlEstimate.AddItem("9 Weeks",8)
gui.frmQuoteCreate.ddlEstimate.AddItem("10 Weeks",9)
gui.frmQuoteCreate.ddlEstimate.AddItem("11 Weeks",10)
gui.frmQuoteCreate.ddlEstimate.AddItem("12 Weeks",11)
gui.frmQuoteCreate.ddlEstimate.AddItem("By Arrangement",12)
gui.frmQuoteCreate.ddlEstimate.AddItem("Delivery as Described Above",13)
Program.Sub.loadExpiration.End

Program.Sub.loadValidity.Start
gui.frmQuoteCreate.ddlExpiration.AddItem("",0)
gui.frmQuoteCreate.ddlExpiration.AddItem("7 days",0)
gui.frmQuoteCreate.ddlExpiration.AddItem("30 days",1)
gui.frmQuoteCreate.ddlExpiration.AddItem("60 days",2)
gui.frmQuoteCreate.ddlExpiration.AddItem("120 days",3)
Program.Sub.loadValidity.End

Program.Sub.cmdBrowse_Click.Start
V.Local.sRet.Declare(String)
V.Local.sTitles.Declare(String)
V.Local.iWidths.Declare(String)
V.Local.ssql.Declare(String)

F.intrinsic.string.split("Customer*!*Name Customer","*!*",V.local.sTitles)
F.intrinsic.string.split("2000*!*3000","*!*",V.local.iWidths)

V.Local.ssql.set("select Customer, NAME_CUSTOMER from V_CUSTOMER_MASTER")

F.intrinsic.ui.browser("Select a Customer",con,v.Local.ssql,V.local.sTitles,V.local.iWidths,V.local.sRet)

F.intrinsic.control.if(V.local.sRet,"=","***CANCEL***")
F.intrinsic.control.else
	F.intrinsic.string.split(V.local.sRet,"*!*",V.local.sRet)
	gui.frmQuoteCreate.txtCustomer.Text(v.Local.sRet(0))
F.Intrinsic.Control.endif
Program.Sub.cmdBrowse_Click.End

Program.Sub.DragDrop.Start
Program.Sub.DragDrop.End

Program.Sub.PostUpdate.Start
v.Local.ssql.Declare(string)
v.Local.sValid.Declare(string)
v.Local.sEstimate.Declare(string)
v.Local.sPayment.Declare(string)
v.Local.sPayment.set(v.Screen.frmQuoteCreate!ddlPayment.value)
v.Local.sEstimate.Set(v.Screen.frmQuoteCreate!ddlEstimate.Value)
v.Local.sValid.Set(v.Screen.frmQuoteCreate!ddlExpiration.value)
f.Intrinsic.String.Build("UPDATE QUOTE_HEADER SET TERMS='{0}', MARK_INFO='{1}', EXPIRATION='{2}' WHERE RECORD_NO='  A '",v.Local.sPayment,v.Local.sEstimate,v.Local.sValid,v.Local.ssql)
f.ODBC.Connection!con.execute(v.Local.ssql)
Program.Sub.PostUpdate.End

Program.Sub.cmdUpload_Click.Start
f.Intrinsic.Control.Try
	'Set Default Variables
	v.Local.sFiles.Declare(String)
	v.Local.sContent.Declare(string)
	v.Local.sRowDetail.Declare(string)
	v.Local.sNewPartNumber.Declare(string)
	v.Local.sPartSuffix.Declare(string)
	v.Local.sdescription.Declare(string)
	v.Local.sHeaderDetail.Declare(string)
	v.Local.sret.Declare(string)
	v.Local.sQuote.Declare(string)
	v.Local.sLine.Declare(string)
	v.Local.sWC.Declare(string)
	v.Local.sWCDesc.Declare(string)
	v.Local.sCost.Declare(string)
	v.Local.sSetupTime.Declare(string)
	v.Local.sRunTime.Declare(string)
	v.Local.sUM.Declare(string)
	v.Local.sMatCost.Declare(string)
	v.Local.sNewContents.Declare(string)
	V.Local.STEMPFILE.Declare(STRING)
	v.Local.sfilename.Declare(string)
	v.Local.scustomer.Declare(string)
	v.Local.sfiledir.Declare(string)
	v.Local.sfile.Declare(string)
	v.Local.sparams.Declare(string)
	v.Local.stemp.Declare(string)
	v.Local.ssql.Declare(string)
	V.Local.sMessage.Declare(string)
	v.Local.stype.Declare(string)
	V.Local.sGSSPart.Declare(string)
	V.Local.sEstimate.Declare(string)
	v.Local.sPromiseDate.Declare(string)
	v.Local.sPL.Declare(string)
	v.Local.sRouting.Declare(string)
	v.Local.sUser1.Declare(string)
	v.Local.sUser2.Declare(string)
	v.Local.sPartAbbr.Declare
	v.Local.sOrderNo.Declare
	v.Local.sAlt.Declare
	
	
	v.Local.i.Declare(Long,0)
	v.Local.j.Declare(long)
	v.Local.x.Declare(long)
	v.Local.xPlusOne.Declare(long)
	V.Local.lOBOB.Declare(long)
	v.Local.lret.Declare(long)
	v.Local.isequence.Declare(long)
	v.Local.lWeek.Declare(long)
	v.Local.lPartNumber.declare(long)
	V.Local.fTimeConv.Declare(Float)
	v.Local.fQty.Declare(float)
	v.Local.fPrice.Declare(float)
	v.Local.fExten.Declare
	
	v.Local.dDueDate.Declare(date,v.Ambient.Now)
	
	v.Local.bCheck.Declare(Boolean)
	v.Local.bQuote.Declare(Boolean)
	v.Local.bSales.Declare(Boolean)
	v.Local.bEstimate.Declare(Boolean)
	
	v.Local.sQuote.Set("")
	v.Local.scustomer.Set(v.Screen.frmQuoteCreate!txtCustomer.text)
	
	v.Local.sEstimate.Set(v.Screen.frmQuoteCreate!ddlEstimate.value)
	f.Intrinsic.String.Split(v.Local.sEstimate," ",v.Local.sEstimate)
	f.Intrinsic.Math.IsNumeric(v.Local.sEstimate(0),v.Local.bEstimate)
	f.Intrinsic.Control.If(v.Local.bEstimate)
		v.Local.lWeek.Set(v.Local.sEstimate(0))
		f.Intrinsic.Math.Mult(v.Local.lWeek,7,v.Local.lWeek)
		f.Intrinsic.Date.DateAdd("D",v.Local.lWeek,v.Ambient.Date,v.Local.dDueDate)
		f.Intrinsic.String.Format(v.Local.dDueDate,"YYYYMMDD",v.Local.sPromiseDate)
	f.Intrinsic.Control.Else
		f.Intrinsic.String.Format(v.Ambient.Date,"YYYYMMDD",v.Local.sPromiseDate)
	f.Intrinsic.Control.EndIf
	
	
	f.Intrinsic.UI.InvokeWaitDialog("Processing...")
	v.Local.sFiles.Set(v.Screen.frmQuoteCreate!txtFileName.text)
	f.Intrinsic.Control.For(v.Local.i,0,v.Local.sFiles.UBound,1)
		'Check if multiple files
		f.Intrinsic.Control.If(v.Local.sFiles.UBound,>,0)
			f.Intrinsic.UI.Msgbox("Multiple files loaded, please select one.")
			f.Intrinsic.UI.CloseWaitDialog
			f.Intrinsic.Control.ExitSub
		f.Intrinsic.Control.EndIf
		
		'Determine if qoute or sales order
		f.Intrinsic.Control.If(v.Screen.frmQuoteCreate!optQuote.Value,=,"True")
			v.Local.stype.Set("Q")
			v.Local.bQuote.Set(true)
		f.Intrinsic.Control.ElseIf(v.Screen.frmQuoteCreate!optSale.Value,=,"True")
			v.Local.stype.Set("O")
			v.Local.bSales.Set(true)
		f.Intrinsic.Control.EndIf
				
		'Set the order header number
		f.Intrinsic.String.LPad(v.Global.lStartNumber,"0",7,v.Local.sOrderNo)
		f.Data.DataTable.AddRow("6004Header","Type",v.Local.stype,"CustNum",v.Local.scustomer.Trim,"OrderNum",v.local.sOrderNo)
		f.Intrinsic.Math.Add(v.Global.lStartNumber,1,v.Global.lStartNumber)
		
		'Get contents from file
		f.Intrinsic.File.File2String(v.Local.sFiles(V.Local.i),v.Local.sContent)
		f.Intrinsic.String.Split(v.Local.sContent,v.Ambient.NewLine,v.Local.sContent)
		f.Intrinsic.Math.Sub(v.Local.scontent.UBound,1,v.Local.lOBOB)
		
		f.Intrinsic.Control.For(v.Local.j,1,v.Local.lOBOB,1)
			F.Intrinsic.Control.If(v.local.sContent(v.Local.j).trim,<>,"")
				'Split contents
				f.Intrinsic.String.SplitCSV(v.Local.sContent(v.Local.j),v.Local.sRowDetail)		
				
				'Set values
				v.Local.fQty.Set(v.Local.sRowDetail(4))
				v.Local.fPrice.Set(v.Local.sRowDetail(6))
				v.Local.sdescription.Set(v.Local.sRowDetail(3))
				v.Local.sUser1.Set(v.Local.sRowDetail(9))
				v.Local.sUser2.Set(v.Local.sRowDetail(10))
				v.Local.sUM.Set(v.Local.sRowDetail(5))
				V.Local.sRouting.Set(v.Local.sRowDetail(1))
				f.Intrinsic.String.Concat(v.Local.sUser1,"@",v.Local.sUser2,v.Local.sAlt)
				f.Intrinsic.String.Concat(v.Caller.CompanyCode,v.Global.lStartNumber,"-",v.Local.j,v.Local.sGSSPart)
				f.Intrinsic.String.LPad(v.Local.j,"0",3,v.Local.sLine)
				f.Global.Inventory.AddPartToBatch(v.Local.sGSSPart,v.Local.sUM,v.Local.sdescription,"","FG","F",v.Local.sRouting,v.Local.salt,v.Local.fPrice)
				f.Intrinsic.Math.Mult(v.Local.fPrice,v.Local.fQty,v.Local.fExten)
				f.Intrinsic.Math.Mult(v.Local.fQty,10000,v.Local.fQty)
				f.Intrinsic.Math.Mult(v.Local.fPrice,1000000,v.Local.fPrice)
				F.Intrinsic.Math.Mult(V.Local.fExten,1000000,v.Local.fExten)
				f.Data.DataTable.AddRow("6004Lines","QuotedPrice",v.Local.fPrice,"QtyOrdered",v.Local.fQty,"ExtendedAmount",v.Local.fExten,"Part",v.Local.sGSSPart,"PartDescription",v.Local.sdescription,"UM",v.Local.sUM,"OrderNum",v.local.sOrderNo,"LineNumber",v.Local.sLine,"PL","FG","User1",v.Local.sUser1,"User2",v.Local.sUser2)
			f.Intrinsic.Control.EndIf
		f.Intrinsic.Control.Next(v.Local.j)
	f.Intrinsic.Control.Next(V.Local.i)
	
	'Post the part batch and update the text one field
	f.global.Inventory.PostPartBatch
	f.Intrinsic.String.Concat(v.Caller.CompanyCode,v.Global.lStartNumber,v.Local.sPartAbbr)
	f.Intrinsic.String.Build("UPDATE INVENTORY_MST2 SET TEXT_INFO1=DESCRIPTION_2 WHERE LEFT(PART,9)='{0}'",v.Local.sPartAbbr,v.Local.ssql)
	F.ODBC.Connection!con.EXECUTE(v.Local.ssql)
	
	'Update the config file
	f.Data.DataTable.SetValue("dtConfig",0,"StartNumber",v.Global.lStartNumber)
	f.Data.DataTable.SaveToXML("dtConfig",0,true,v.Global.sConfigFile)

	f.Intrinsic.Control.If(v.Local.bQuote,=,true,"or",v.Local.bSales,=,true)
		'Check if customer exist
		v.Local.ssql.Set("SELECT CUSTOMER FROM V_CUSTOMER_MASTER WHERE CUSTOMER='{0}'")
		F.Intrinsic.String.Build(v.Local.ssql,v.Local.scustomer.Trim,v.Local.ssql)
		f.ODBC.Connection!con.executeandreturn(v.Local.ssql,v.Local.sret)
		f.Intrinsic.Control.If(v.Local.sret.Trim,=,"")
			f.Intrinsic.UI.Msgbox("Customer not found, please try again.")
			F.Intrinsic.UI.CloseWaitDialog
			f.Intrinsic.Control.ExitSub
		f.Intrinsic.Control.EndIf
		
		'Run the upload
		f.Intrinsic.Control.CallSub(6004Sync)
		
		F.Intrinsic.String.Concat(V.Caller.FilesDir,"\","ORDQTE.txt",v.Local.stemp)
		f.Intrinsic.File.File2String(v.Local.stemp,v.Local.squote)
		f.Intrinsic.Math.IsNumeric(v.Local.sQuote,v.Local.bCheck)
'		
		f.Intrinsic.Control.If(v.Local.bCheck,=,false)
			f.Intrinsic.UI.Msgbox("No quote created, see error log.  Program will now exit.")
			f.Intrinsic.UI.CloseWaitDialog
			F.ODBC.Connection!con.Close
			f.Intrinsic.Control.End
		f.Intrinsic.Control.EndIf
		
		f.Intrinsic.UI.CloseWaitDialog
		
		'Open the Quote Form
		f.Intrinsic.Control.If(v.Local.bQuote,=,true)
			F.Intrinsic.String.Concat(V.Local.sQuote,"!*!O",v.Local.sparams)
			f.Global.General.CallWrapperAsync(251000,v.local.sparams)
		f.Intrinsic.Control.EndIf
		
		f.Intrinsic.Control.If(v.Local.bSales,=,true)
			F.Intrinsic.String.Concat(V.Local.sQuote,"!*!O!*!",v.Local.scustomer,v.Local.sparams)
			f.Global.General.CallWrapperAsync(200000,v.local.sparams)
		f.Intrinsic.Control.EndIf
			
	f.Intrinsic.Control.EndIf

	f.ODBC.Connection!con.Close
	f.Intrinsic.Control.End
f.Intrinsic.Control.Catch
	f.Intrinsic.UI.Msgbox(v.Ambient.ErrorDescription)
	f.ODBC.Connection!con.Close
	f.Intrinsic.Control.End
f.Intrinsic.Control.EndTry
Program.Sub.cmdUpload_Click.End

Program.Sub.cmdBrowseFile_Click.Start
f.Intrinsic.Control.Try
	v.Local.sret.Declare
	Function.Intrinsic.UI.ShowOpenFileDialog("","CSV File|*.csv","My Documents",v.Local.sret)
	F.Intrinsic.Control.If(v.Local.sret,<>,"***CANCEL***")
		gui.frmQuoteCreate.txtFileName.Text(v.Local.sret)
	F.Intrinsic.Control.EndIf
f.Intrinsic.Control.Catch
	f.Intrinsic.UI.Msgbox(v.Ambient.ErrorDescription)
	f.ODBC.Connection!con.Close
	f.Intrinsic.Control.End
f.Intrinsic.Control.EndTry
Program.Sub.cmdBrowseFile_Click.End
